<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
    xmlns:mods="http://www.loc.gov/mods/v3"
    version="1.0">
    <xsl:output encoding="UTF-8" method="xml" indent="yes"/>

    <!-- Mapping from FOLIO raw format to mods for the FOLIO Z39.50/SRU server 
         Marko Knepper, UB Mainz 2025, Apache 2.0 -->

    <xsl:template match="opt|record">
        <mods:mods xsi:schemaLocation="http://www.loc.gov/mods/v3 http://www.loc.gov/standards/mods/v3/mods-3-8.xsd">
            <xsl:apply-templates mode="instance"/>
            <mods:recordInfo>
                <mods:recordCreationDate encoding="iso8601"><xsl:value-of select="metadata/createdDate"/></mods:recordCreationDate>
                <mods:recordChangeDate encoding="iso8601"><xsl:value-of select="metadata/updatedDate"/></mods:recordChangeDate>
                <xsl:if test="source/text()"><mods:recordIdentifier source="{source}"><xsl:value-of select="hrid"/></mods:recordIdentifier></xsl:if>
                <mods:recordIdentifier source="uuid"><xsl:value-of select="id"/></mods:recordIdentifier>
                <mods:recordIdentifier source="hrid"><xsl:value-of select="hrid"/></mods:recordIdentifier>
            </mods:recordInfo>
        </mods:mods>
    </xsl:template>

    <xsl:template match="holdingsRecords2" mode="instance">
        <mods:location>
            <mods:physicalLocation>
                <xsl:value-of select="permanentLocation/institution/name"/><xsl:text>, </xsl:text>
                <xsl:value-of select="permanentLocation/library/name"/>
            </mods:physicalLocation>
            <xsl:choose>
                <xsl:when test="bareHoldingsItems">
                    <mods:holdingSimple>
                        <xsl:apply-templates select="bareHoldingsItems" mode="holdings"/>                    
                    </mods:holdingSimple>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:apply-templates select="callNumber" mode="holdings"/>
                </xsl:otherwise>
            </xsl:choose>
            <xsl:if test="hrid/text()">
                <mods:holdingExternal>
                    <identifier type="hrid"><xsl:value-of select="hrid"/></identifier>
                </mods:holdingExternal>
            </xsl:if>
        </mods:location>
    </xsl:template>

    <xsl:template match="title" mode="instance">
        <mods:titleInfo>
            <mods:title><xsl:value-of select="."/></mods:title>
        </mods:titleInfo>
    </xsl:template>
    
    <xsl:template match="contributors" mode="instance">
        <mods:name>
            <mods:displayForm><xsl:value-of select="name"/></mods:displayForm>
        </mods:name>
    </xsl:template>
    
    <xsl:template match="publication" mode="instance">
        <mods:originInfo>
            <mods:place>
                <mods:placeTerm type="text"><xsl:value-of select="place"/></mods:placeTerm>
            </mods:place>
            <mods:publisher><xsl:value-of select="publisher"/></mods:publisher>
            <mods:dateIssued keyDate="yes"><xsl:value-of select="dateOfPublication"/></mods:dateIssued>
        </mods:originInfo>
    </xsl:template>
   
    <xsl:template match="classifications" mode="instance">
        <xsl:choose> <!-- covering some of the reference data -->
            <xsl:when test="classificationTypeId='ce176ace-a53e-4b4d-aa89-725ed7b2edac'">
                <mods:classification authority="ce176ace-a53e-4b4d-aa89-725ed7b2edac" displayLabel="LCC" authorityURI="http://id.loc.gov/vocabulary/classSchemes/lcc">
                    <xsl:value-of select="classificationNumber"/>
                </mods:classification>
            </xsl:when>
            <xsl:when test="classificationTypeId='42471af9-7d25-4f3a-bf78-60d29dcf463b'">
                <mods:classification authority="42471af9-7d25-4f3a-bf78-60d29dcf463b" displayLabel="DDC" authorityURI="http://id.loc.gov/vocabulary/classSchemes/ddc">
                    <xsl:value-of select="classificationNumber"/>
                </mods:classification>
            </xsl:when>
            <xsl:otherwise>
                <mods:classification authority="{classificationNumber}">
                    <xsl:value-of select="classificationNumber"/>
                </mods:classification>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:template>
    
    <xsl:template match="subjects" mode="instance">
        <mods:subject>
            <mods:topic><xsl:value-of select="value"/></mods:topic>
        </mods:subject>
    </xsl:template>
    
    <xsl:template match="instanceTypeId" mode="instance">
        <!-- Auto-generated by Python script -->
        <xsl:choose>
            <xsl:when test=".='3363cdb1-e644-446c-82a4-dc3a1d4395b9'">
                <mods:genre>cartographic dataset</mods:genre>
            </xsl:when>
            <xsl:when test=".='526aa04d-9289-4511-8866-349299592c18'">
                <mods:genre>cartographic image</mods:genre>
            </xsl:when>
            <xsl:when test=".='80c0c134-0240-4b63-99d0-6ca755d5f433'">
                <mods:genre>cartographic moving image</mods:genre>
            </xsl:when>
            <xsl:when test=".='408f82f0-e612-4977-96a1-02076229e312'">
                <mods:genre>cartographic tactile image</mods:genre>
            </xsl:when>
            <xsl:when test=".='e5136fa2-1f19-4581-b005-6e007a940ca8'">
                <mods:genre>cartographic tactile three-dimensional form</mods:genre>
            </xsl:when>
            <xsl:when test=".='2022aa2e-bdde-4dc4-90bc-115e8894b8b3'">
                <mods:genre>cartographic three-dimensional form</mods:genre>
            </xsl:when>
            <xsl:when test=".='df5dddff-9c30-4507-8b82-119ff972d4d7'">
                <mods:genre>computer dataset</mods:genre>
            </xsl:when>
            <xsl:when test=".='c208544b-9e28-44fa-a13c-f4093d72f798'">
                <mods:genre>computer program</mods:genre>
            </xsl:when>
            <xsl:when test=".='fbe264b5-69aa-4b7c-a230-3b53337f6440'">
                <mods:genre>notated movement</mods:genre>
            </xsl:when>
            <xsl:when test=".='497b5090-3da2-486c-b57f-de5bb3c2e26d'">
                <mods:genre>notated music</mods:genre>
            </xsl:when>
            <xsl:when test=".='a2c91e87-6bab-44d6-8adb-1fd02481fc4f'">
                <mods:genre>other</mods:genre>
            </xsl:when>
            <xsl:when test=".='3be24c14-3551-4180-9292-26a786649c8b'">
                <mods:genre>performed music</mods:genre>
            </xsl:when>
            <xsl:when test=".='9bce18bd-45bf-4949-8fa8-63163e4b7d7f'">
                <mods:genre>sounds</mods:genre>
            </xsl:when>
            <xsl:when test=".='c7f7446f-4642-4d97-88c9-55bae2ad6c7f'">
                <mods:genre>spoken word</mods:genre>
            </xsl:when>
            <xsl:when test=".='535e3160-763a-42f9-b0c0-d8ed7df6e2a2'">
                <mods:genre>still image</mods:genre>
            </xsl:when>
            <xsl:when test=".='efe2e89b-0525-4535-aa9b-3ff1a131189e'">
                <mods:genre>tactile image</mods:genre>
            </xsl:when>
            <xsl:when test=".='e6a278fb-565a-4296-a7c5-8eb63d259522'">
                <mods:genre>tactile notated movement</mods:genre>
            </xsl:when>
            <xsl:when test=".='a67e00fd-dcce-42a9-9e75-fd654ec31e89'">
                <mods:genre>tactile notated music</mods:genre>
            </xsl:when>
            <xsl:when test=".='8105bd44-e7bd-487e-a8f2-b804a361d92f'">
                <mods:genre>tactile text</mods:genre>
            </xsl:when>
            <xsl:when test=".='82689e16-629d-47f7-94b5-d89736cf11f2'">
                <mods:genre>tactile three-dimensional form</mods:genre>
            </xsl:when>
            <xsl:when test=".='6312d172-f0cf-40f6-b27d-9fa8feaf332f'">
                <mods:genre>text</mods:genre>
            </xsl:when>
            <xsl:when test=".='c1e95c2b-4efc-48cf-9e71-edb622cf0c22'">
                <mods:genre>three-dimensional form</mods:genre>
            </xsl:when>
            <xsl:when test=".='3e3039b7-fda0-4ac4-885a-022d457cb99c'">
                <mods:genre>three-dimensional moving image</mods:genre>
            </xsl:when>
            <xsl:when test=".='225faa14-f9bf-4ecd-990d-69433c912434'">
                <mods:genre>two-dimensional moving image</mods:genre>
            </xsl:when>
            <xsl:when test=".='30fffe0e-e985-4144-b2e2-1e8179bdb41f'">
                <mods:genre>unspecified</mods:genre>
            </xsl:when>
        </xsl:choose>
    </xsl:template>
   
    <!-- covering some of the reference data -->
    <xsl:template match="identifiers" mode="instance">
        <xsl:choose>
            <xsl:when test="identifierTypeId='8261054f-be78-422d-bd51-4ed9f33c3422'">
                <mods:identifier type="8261054f-be78-422d-bd51-4ed9f33c3422" displayLabel="ISBN" typeURI="http://id.loc.gov/vocabulary/identifiers/isbn">
                    <xsl:value-of select="value"/>
                </mods:identifier>
            </xsl:when>
            <xsl:when test="identifierTypeId='913300b2-03ed-469a-8179-c1092c991227'">
                <mods:identifier type="913300b2-03ed-469a-8179-c1092c991227" displayLabel="ISSN" typeURI="http://id.loc.gov/vocabulary/identifiers/issn">
                    <xsl:value-of select="value"/>
                </mods:identifier>
            </xsl:when>
            <xsl:when test="identifierTypeId='ebfd00b6-61d3-4d87-a6d8-810c941176d5'">
                <mods:identifier type="ebfd00b6-61d3-4d87-a6d8-810c941176d5" displayLabel="ISMN" typeURI="http://id.loc.gov/vocabulary/identifiers/ismm">
                    <xsl:value-of select="value"/>
                </mods:identifier>
            </xsl:when>
            <xsl:when test="identifierTypeId='39554f54-d0bb-4f0a-89a4-e422f6136316'">
                <mods:identifier type="39554f54-d0bb-4f0a-89a4-e422f6136316" displayLabel="DOI" typeURI="http://id.loc.gov/vocabulary/identifiers/doi">
                    <xsl:value-of select="value"/>
                </mods:identifier>
            </xsl:when>
            <xsl:otherwise>
                <mods:identifier type="{identifierTypeId}">
                    <xsl:value-of select="value"/>
                </mods:identifier>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:template>
    
    <xsl:template match="languages" mode="instance">
        <mods:language>
            <mods:languageTerm type="code" authority="iso639-2b"><xsl:value-of select="."/></mods:languageTerm> 
        </mods:language>   
    </xsl:template>
   
    <xsl:template match="callNumber[text()]" mode="holdings">
        <mods:shelfLocator>
            <xsl:if test="../callNumberPrefix/text()"><xsl:value-of select="../callNumberPrefix"/><xsl:text> </xsl:text></xsl:if>
            <xsl:value-of select="."/>
            <xsl:if test="../callNumberSuffix/text()"><xsl:text> </xsl:text><xsl:value-of select="../callNumberSuffix"/></xsl:if>
        </mods:shelfLocator>
    </xsl:template>
   
    <xsl:template match="bareHoldingsItems" mode="holdings"> <!-- mapping each item of all holdings on mods:copyInformation -->
        <mods:copyInformation>
            <xsl:apply-templates select="materialType" mode="item"/>
            <xsl:choose>         <!-- permanentLocation is inherited here -->
                <xsl:when test="permanentLocation/name/text()">
                    <xsl:apply-templates select="permanentLocation" mode="item"/>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:apply-templates select="../permanentLocation" mode="item"/>
                </xsl:otherwise>
            </xsl:choose>
            <xsl:apply-templates select="effectiveCallNumberComponents" mode="item"/>
            <xsl:apply-templates select="../notes" mode="item"/>
            <xsl:apply-templates select="chronology|copyNumber" mode="item"/>
            <xsl:apply-templates select="barcode|hrid" mode="item"/>
        </mods:copyInformation>
    </xsl:template>
    
    <xsl:template match="effectiveCallNumberComponents" mode="item">
        <mods:shelfLocator>
            <xsl:if test="prefix/text()"><xsl:value-of select="prefix"/><xsl:text> </xsl:text></xsl:if>
            <xsl:value-of select="callNumber"/>
            <xsl:if test="suffix/text()"><xsl:text> </xsl:text><xsl:value-of select="suffix"/></xsl:if>
        </mods:shelfLocator>
    </xsl:template>
    
    <xsl:template match="permanentLocation" mode="item">
        <mods:subLocation><xsl:value-of select="name"/></mods:subLocation>
    </xsl:template>
    
    <xsl:template match="barcode" mode="item">
        <mods:itemIdentifier type="barcode"><xsl:value-of select="."/></mods:itemIdentifier>
    </xsl:template>
    
    <xsl:template match="hrid" mode= "item">
        <mods:itemIdentifier type="hrid"><xsl:value-of select="."/></mods:itemIdentifier>
    </xsl:template>
    
    <xsl:template match="materialType" mode="item">
        <mods:form><xsl:value-of select="name"/></mods:form>
    </xsl:template>
    
    <xsl:template match="notes" mode="item">
        <mods:note type="{holdingsNoteType/name}"><xsl:value-of select="note"/></mods:note>
    </xsl:template>

    <xsl:template match="chronology[text()]|copyNumber[text()]" mode="item">
        <mods:enumerationAndChronology unitType="1"><xsl:value-of select="."/></mods:enumerationAndChronology>
    </xsl:template>

    <xsl:template match="text()" mode="instance"/>
</xsl:stylesheet>
